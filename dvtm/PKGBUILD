# $Id: PKGBUILD 122719 2014-11-18 20:39:26Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: bender02 at gmx dot com

pkgname=dvtm
pkgver=0.14
pkgrel=1
pkgdesc='Dynamic virtual terminal manager.'
arch=('i686' 'x86_64')
url='http://www.brain-dump.org/projects/dvtm/'
groups=('modified')
license=('MIT')
depends=('ncurses')
provides=('dvtm')
conflicts=('dvtm-git')
source=("http://www.brain-dump.org/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'config.h')
md5sums=('44ec5176b1996fdffd97cabe9a35ea0c'
         'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	cp "${srcdir}/config.h" .
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make clean
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
