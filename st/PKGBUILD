# Maintainer: Eric Mrak <mail at ericmrak dot info>
# vim:set ts=2 sw=2 et:
pkgname=st
pkgver=0.6
pkgrel=3
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
groups=('modified')
license=('MIT')
depends=('libxext' 'libxft')
makedepends=('ncurses')
url="http://st.suckless.org"
source=(config.h
        http://st.suckless.org/patches/st-$pkgver-hidexcursor.diff
        http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz)
md5sums=('SKIP'
         'fd30c59d587f91a1dd5f82cd2ceb752c'
         '1a926f450b4eacb7e2f5ac5b8ffea7c8')

build() {
  cd $srcdir/$pkgname-$pkgver
  cp $srcdir/config.h .
  patch -i $srcdir/st-$pkgver-hidexcursor.diff
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  cp $srcdir/config.h .
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  rm "$pkgdir/usr/share/terminfo/s/st"
  rm "$pkgdir/usr/share/terminfo/s/st-256color"
}

