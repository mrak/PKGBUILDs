# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.6
pkgrel=3
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
groups=('modified')
license=('MIT')
depends=('libxft' 'libxext')
makedepends=('ncurses')
url="http://st.suckless.org"
source=(http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz
        http://st.suckless.org/patches/st-$pkgver-hidexcursor.diff
        config.h)
md5sums=('1a926f450b4eacb7e2f5ac5b8ffea7c8'
         'fd30c59d587f91a1dd5f82cd2ceb752c'
         'SKIP')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  # skip terminfo which conflicts with nsurses
  sed -i '/\@tic /d' Makefile
  cp $srcdir/config.h config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -i $srcdir/st-$pkgver-hidexcursor.diff
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
