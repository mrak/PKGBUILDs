# Maintainer: Eric Mrak <info AT ericmrak DOT info>

_hkgname=hdevtools
pkgname=haskell-$_hkgname
pkgver=0.1.0.5
pkgrel=1
pkgdesc="Persistent GHC powered background server for FAST Haskell development tools"
url="http://hackage.haskell.org/package/hdevtools"
license=('MIT')
arch=('i686' 'x86_64')
depends=('ghc')
makedepends=('cabal-install')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz)
md5sums=('ed28102c075e362c4e8bc4b64dcd4fc2')

build() {
    cd "$srcdir/$_hkgname-$pkgver"

    cabal sandbox init
    cabal update
    cabal install --only-dependencies
    cabal configure --prefix=/usr \
                    --enable-split-objs \
                    --enable-shared \
                    --docdir="/usr/share/doc/$pkgname" \
                    --libsubdir=\$compiler/site-local/\$pkgid
    cabal build
}

package() {
    cd "$srcdir/$_hkgname-$pkgver"
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
    cabal copy --destdir=${pkgdir}
}

