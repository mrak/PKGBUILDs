# $Id: PKGBUILD 106937 2014-03-10 18:31:13Z jelle $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=xmobar
pkgver=0.23
pkgrel=1
pkgdesc="A Minimalistic Text Based Status Bar"
url="http://hackage.haskell.org/package/xmobar"
license=('custom:BSD3')
groups=('modified')
arch=('i686' 'x86_64')
depends=('gmp' 'libxft' 'libxinerama' 'libxrandr' 'dbus' 'libxpm')
makedepends=('ghc' 'cabal-install')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/xmobar/$pkgver/xmobar-$pkgver.tar.gz)
md5sums=('5ab73783a6a8f97753a263652e1c141c')

build() {
    cd ${srcdir}/xmobar-$pkgver

    cabal sandbox init
    cabal update
    cabal install --only-dependencies
    cabal install dbus
    cabal configure --prefix=/usr --flags="with_utf8 with_xft with_dbus with_threaded with_xpm"
    cabal build
}

package() {
    cd ${srcdir}/xmobar-$pkgver
    cabal copy --destdir=${pkgdir}
    install -D -m644 license ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

